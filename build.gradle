plugins {
    id 'java'
    id "com.diffplug.spotless" version "7.0.0.BETA2"
    id "com.gradleup.shadow" version "8.3.1"
}

group = 'org.shadow'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes('Main-Class': 'org.shadow.Main')
    }
}

dependencies {
    implementation 'com.typesafe:config:1.4.3'
    implementation platform('org.apache.logging.log4j:log4j-bom:2.23.1')
    implementation 'org.apache.logging.log4j:log4j-core'
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.2'
    implementation 'org.apache.commons:commons-collections4:4.4'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.13.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.13.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        googleJavaFormat() // Use Google's Java formatter
        target 'src/**/*.java' // Specify which files to format
    }
}

tasks.register('copyHistoricalDataToResources', Copy) {
    from 'historical-data'
    into 'src/main/resources/historical-data'

    doFirst {
        println 'Copying historical-data to resources for development mode...'
    }
}

tasks.register('runInDevelopmentMode', JavaExec) {
    mainClass = 'org.shadow.Main'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ['-Dconfig.file=src/main/resources/application.conf.dev']

    processResources.dependsOn copyHistoricalDataToResources
    dependsOn 'copyHistoricalDataToResources'

    doLast {
        println 'Cleaning up historical-data from resources...'
        delete fileTree('src/main/resources/historical-data')
    }
}